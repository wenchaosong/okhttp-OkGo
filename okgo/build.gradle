plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.lzy.okgo'
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'GoogleAppIndexingWarning'
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }
}

publishing {
    repositories { RepositoryHandler handler ->
        handler.maven {
            url '../repo'
        }
    }
    publications { PublicationContainer publication ->
        maven(MavenPublication) {
            afterEvaluate { artifact(tasks.getByName('bundleReleaseAar')) }
            groupId = 'lib.http'
            artifactId = 'okgo'
            version = '1.0.0'

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.group != null && (it.name != null && "unspecified" != it.name) && it.version != null) {
                        println it.toString()
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        dependencyNode.appendNode('scope', 'implementation')
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.10'
}
